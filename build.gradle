import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'com.bmuschko.docker-remote-api' version '4.6.2'
}

apply plugin: 'io.spring.dependency-management'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

group = 'com.github.dddpaul'
version = '2.1.1.RELEASE'
sourceCompatibility = JavaVersion.VERSION_11

ext {
    dockerPrefix = 'dddpaul'
    springCloudVersion = 'Greenwich.SR1'
    junitVersion = '5.3.1'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

bootJar {
    mainClassName = 'com.github.dddpaul.spring.SpringCloudConfigServerApplication'
}

task dockerCreateFile(type: Dockerfile) {
    dependsOn bootJar
    destFile = file("${project.buildDir}/Dockerfile")
    from 'adoptopenjdk/openjdk11:jdk-11.0.2.9-alpine-slim'
    addFile "libs/${project.name}-${project.version}.jar", '/app.jar'
    volume '/config'
    exposePort 8888
    entryPoint 'java', '-Djava.security.egd=file:/dev/./urandom', '-jar', '/app.jar', '--spring.config.name=configserver', '--spring.cloud.config.server.encrypt.enabled=false'
}

task dockerBuildImage(type: DockerBuildImage) {
    dependsOn dockerCreateFile
    inputDir = file("${project.buildDir}")
    tags = [
            "${dockerPrefix}/${project.name}".toString(),
            "${dockerPrefix}/${project.name}:${project.version}".toString()
    ]
}

dependencies {
    implementation 'org.springframework.cloud:spring-cloud-config-server:2.1.1.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

}

wrapper {
    gradleVersion = '5.2.1'
    distributionUrl = 'https://services.gradle.org/distributions/gradle-5.2.1-bin.zip'
}
